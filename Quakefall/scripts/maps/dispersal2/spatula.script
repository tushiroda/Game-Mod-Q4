namespace spatulaScript
{
float pauseLoop = 0;

void InitSpatula(entity leftSpatulaDoor, entity rightSpatulaDoor, entity spatula, entity spatulaArm)
{
	leftSpatulaDoor.bind(spatula);
	rightSpatulaDoor.bind(spatula);
	spatula.bind(spatulaArm);
}

void RetractSpatula(entity spatulaArm, entity spatulaRetracted)
{
	spatulaArm.speed(55);
	spatulaArm.moveTo(spatulaRetracted);
	sys.waitFor(spatulaArm);
}

void ExtendSpatula(entity spatulaArm, entity spatulaExtended)
{
	spatulaArm.speed(55);
	spatulaArm.moveTo(spatulaExtended);
	sys.waitFor(spatulaArm);
}

void MoveBodyOntoBelt(entity spatula)
{
	spatula.time(2);
	spatula.rotateOnce('-90 0 0');
	sys.waitFor(spatula);
	sys.wait(3);
	spatula.rotateOnce('90 0 0');
	sys.waitFor(spatula);
}

void BodyHitTheSpatula()
{
	sys.trigger($splatSpeaker);
}

void BodyHitTheSpatula2()
{
	sys.trigger($splatSpeaker_1);
}


float	numBodies = 0;
void DropBody(entity fallingBodyTemplate, vector fallingBodyOrigin, entity splatSpeaker, entity spatula)
{
	string	bodyName;
	entity	bodyEnt;
	string	templateName;
	float	whichBody = 0;

	numBodies++;
//	sys.trigger(bodyPool);
	templateName = fallingBodyTemplate.getName();
	bodyName = "body" + numBodies;
	//sys.copySpawnArgs(templateName,"body");
	sys.setSpawnVector("origin", fallingBodyOrigin); // "4614 -2999 -4752"
	sys.setSpawnArg("touchtriggers", "1");
	sys.setSpawnArg("name", bodyName);
	
	vector newOrigin;
	vector templateOrigin;
	vector bodyOffset;
	string bodyOffsetStr;
	
	templateOrigin = fallingBodyTemplate.getOrigin();
	bodyOffset = VectorSubtract(fallingBodyOrigin, templateOrigin);
	bodyOffsetStr = bodyOffset;
	sys.setSpawnArg("bodyOffset", bodyOffsetStr);
	bodyEnt = sys.spawn("env_ragdoll_marine_meathook");
	spatula.setKey("bodyToToss",bodyEnt.getName());

	sys.wait(0.01);
	sys.trigger(bodyEnt);
	bodyEnt.becomeRagdoll();
	sys.wait(16);
	
	bodyEnt.remove();
		
}

void OpenSpatulaDoorsPleaseHal(entity furnaceDoor, entity spatulaField1, entity spatulaField2, entity leftSpatulaDoor, entity rightSpatulaDoor, entity spatula )
{
	entity entDropMe = sys.getEntity(spatula.getKey("bodyToToss"));
	//sys.println("Dropping " + entDropMe.getName());
	entDropMe.disableClip();
	// open the doors in the floor
	sys.trigger(furnaceDoor);
	
	//sys.trigger(spatulaField1);
	//sys.trigger(spatulaField2);
	leftSpatulaDoor.time(1);
	leftSpatulaDoor.rotateOnce('0 0 80');
	rightSpatulaDoor.time(1);
	rightSpatulaDoor.rotateOnce('0 0 80');
	sys.waitFor(leftSpatulaDoor);
	
	sys.waitFrame();
	entDropMe.applyImpulse(entDropMe,entDropMe.getWorldOrigin(),'0 0 -200000');

	sys.wait(2);
	leftSpatulaDoor.time(1);
	leftSpatulaDoor.rotateOnce('0 0 -80');
	rightSpatulaDoor.time(1);
	rightSpatulaDoor.rotateOnce('0 0 -80');
	//sys.trigger(spatulaField1);
	//sys.trigger(spatulaField2);
	sys.waitFor(rightSpatulaDoor);
}

void PauseSpatulaLoop()
{
	//sys.println("Spatula loop paused!");
	pauseLoop = 1;
}

void UnPauseSpatulaLoop()
{
	//sys.println("Spatula loop unpaused!");
	pauseLoop = 0;
}

void StartSpatulaLoop(entity spatula, entity spatulaArm, entity leftSpatulaDoor, entity rightSpatulaDoor, entity spatulaExtended, entity spatulaRetracted,
						entity fallingBodyTemplate, vector fallingBodyOrigin, entity furnaceDoor, entity spatulaField1, entity spatulaField2, entity splatSpeaker,
						entity bodyPool)
{
	float r;
	
	InitSpatula(leftSpatulaDoor, rightSpatulaDoor, spatula, spatulaArm);
	while(1)
	{
		if (pauseLoop)
		{
			sys.wait(1);
		}
		else
		{
			thread DropBody(fallingBodyTemplate, fallingBodyOrigin, splatSpeaker, spatula);
			sys.wait(8);
			r = sys.random(1);
			if (r > .7)
			{
				OpenSpatulaDoorsPleaseHal(furnaceDoor, spatulaField1, spatulaField2, leftSpatulaDoor, rightSpatulaDoor, spatula);
			}
			else
			{
				RetractSpatula(spatulaArm, spatulaRetracted);
				sys.wait(1);
				MoveBodyOntoBelt(spatula);
				sys.wait(1);
				ExtendSpatula(spatulaArm, spatulaExtended);
			}
		}
	}
}

} // namespace spatula
